c**********************************************************************
c SWIFT_RMVS3_FP_YE.F
c**********************************************************************
c Includes close encounters. To run, need 7 input files.
c The code prompts for the file names, but examples are: param.in,
c pl.in, tp.in, filter.in, th.in, spin.in, yarko.in
c
c Authors:  Hal Levison \& Martin Duncan
c Date:    8/25/94
c Last revision: 12/27/96

c Remarks: Added thermal effects, filter/decimation process, real*8 output
c   and on-line calculation of proper elements.
c Modified by: Miroslav Broz, miroslav.broz@email.cz
c Date: Feb 14th 2008
     
      include 'swift.inc'

      real*8 xht(NTPMAX),yht(NTPMAX),zht(NTPMAX)
      real*8 vxht(NTPMAX),vyht(NTPMAX),vzht(NTPMAX)

      real*8 mass(NPLMAX),j2rp2,j4rp4
      real*8 xh(NPLMAX),yh(NPLMAX),zh(NPLMAX)
      real*8 vxh(NPLMAX),vyh(NPLMAX),vzh(NPLMAX)

      integer istat(NTPMAX,NSTAT),i1st
      integer nbod,ntp,nleft
      integer iflgchk,iub,iuj,iud,iue,iuf,iur,iui,iup
      real*8 rstat(NTPMAX,NSTATR)

      real*8 t0,tstop,dt,dtout,dtdump,dtfilter,dtproper,dtreorient
      real*8 t,tout,tdump,tfrac,eoff,tfilter,tproper,treorient

      real*8 rmin,rmax,rmaxu,qmin,rplsq(NPLMAX)
      logical lclose 

      character*80 outfile,inparfile,inplfile,intpfile,fopenstat,
     &  infilterfile,outfilterfile,inproperfile,outproperfile,
     &  inthfile,inspinfile,inyarkofile

c  reorientation and disruption variables
      integer iseed
      logical reoriented, yorpiented, disrupted
      real*8 tau_reor(NTPMAX),tau_disr(NTPMAX),tau_yorp(NTPMAX)

c  additional discard criteria
      real*8 a_mean1, a_mean2

c  name of this driver
      character*16 DRIVER
      common /drivername/ DRIVER

cc  OMP timings, debugging
c!$    real *8 omp_get_wtime
c!$    real *8 tbegin, tend
c!$    common /timing/ tbegin, tend

c  main ----------------------------------------------------------
c!$    tbegin = omp_get_wtime()

c...    print version number
      DRIVER = "swift_rmvs3_fp_ye"
      call util_version

c Get data for the run and the test particles
      write(*,*) 'Enter name of parameter data file : '
      read(*,999) inparfile
      call io_init_param(inparfile,t0,tstop,dt,dtout,dtdump,
     &  iflgchk,rmin,rmax,rmaxu,qmin,lclose,outfile,fopenstat)

c Prompt and read name of planet data file
      write(*,*) ' '
      write(*,*) 'Enter name of planet data file : '
      read(*,999) inplfile
999   format(a)
      call io_init_pl(inplfile,lclose,iflgchk,nbod,mass,xh,yh,zh,
     &  vxh,vyh,vzh,rplsq,j2rp2,j4rp4)

c Get data for the run and the test particles
      write(*,*) 'Enter name of test particle data file : '
      read(*,999) intpfile
      call io_init_tp(intpfile,ntp,xht,yht,zht,vxht,vyht,
     &  vzht,istat,rstat)

c Prompt the name and read the filter parameters file
      write(*,*) 'Enter name of filter parameters file : '
      read(*,999) infilterfile
      call io_init_filter(infilterfile,nbod,ntp,dtfilter,outfilterfile,
     :  iflgchk)

c Prompt the name and read the proper parameters file
      write(*,*) 'Enter name of proper parameters file : '
      read(*,999) inproperfile
      call io_init_proper(inproperfile,dtproper,outproperfile,
     :  iflgchk)

c Prompt and read name of thermal data file
      write(*,*) 'Enter name of thermal data file : '
      read(*,999) inthfile
      call io_init_th(inthfile,ntp)

c Read the spin axes data file
      write(*,*) 'Enter name of spin axes data file : '
      read(*,999) inspinfile
      call io_init_spin(inspinfile,ntp,iseed)

c Read other parameters from file like yarko.in
      write(*,*) 'Enter name of yarko-data file : '
      read(*,999) inyarkofile
      call io_init_yarko(inyarkofile,nbod,ntp,dtreorient,
     &  tau_reor,tau_disr,tau_yorp,a_mean1,a_mean2,iflgchk)

c Initialize initial time and times for first output and first dump
      t = t0
      tout = t0 + dtout
      tdump = t0 + dtdump
      tfilter = t0 + dtfilter
      tproper = t0 + dtproper
      treorient = t0 + dtreorient

c io unit numbers
      iub = 20
      iuj = 30
      iud = 40
      iue = 60
      iuf = 70
      iur = 80
      iui = 90
      iup = 75

c  initial io write ----------------------------------------------
      if(btest(iflgchk,8))  then ! bit 8 is set
        call io_write_frame_r8(t0,nbod,ntp,mass,xh,yh,zh,
     &    vxh,vyh,vzh,xht,yht,zht,vxht,vyht,vzht,istat,outfile,iub,
     &    fopenstat)
      else if(btest(iflgchk,0))  then ! bit 0 is set
        call io_write_frame(t0,nbod,ntp,mass,xh,yh,zh,vxh,vyh,vzh,
     &    xht,yht,zht,vxht,vyht,vzht,istat,outfile,iub,fopenstat)
      else if(btest(iflgchk,1))  then ! bit 1 is set
        call io_write_frame_r(t0,nbod,ntp,mass,xh,yh,zh,vxh,vyh,vzh,
     &    xht,yht,zht,vxht,vyht,vzht,istat,outfile,iub,fopenstat)
      endif
      if(btest(iflgchk,2))  then    ! bit 2 is set
        eoff = 0.0d0
        call anal_energy_write(t0,nbod,mass,j2rp2,j4rp4,xh,yh,zh,vxh,
     &    vyh,vzh,iue,fopenstat,eoff)
      endif
      if(btest(iflgchk,3))  then    ! bit 3 is set
        call anal_jacobi_write(t0,nbod,ntp,mass,xh,yh,zh,vxh,
     &    vyh,vzh,xht,yht,zht,vxht,vyht,vzht,istat,2,iuj,fopenstat)
      endif

c...  must initize discard io routine
      if(btest(iflgchk,4))  then ! bit 4 is set
        call io_discard_write(0,t,nbod,ntp,xh,yh,zh,vxh,vyh,
     &    vzh,xht,yht,zht,vxht,vyht,vzht,istat,rstat,iud,
     &    'discard.out',fopenstat,nleft)
      endif

c  do initial dump of filtered elements (we need to initialize elmts array
c  for getacc_tp.f subroutine)
      call io_write_filter(t,nbod,ntp,mass,xh,yh,zh,
     &  vxh,vyh,vzh,xht,yht,zht,vxht,vyht,vzht,istat,
     &  outfilterfile,iuf,fopenstat)
  
c  calculate parameters for getacc_yarko subroutine (Yarkovsky seasonal force)
      call yarko_seasonal(ntp,mass)

c  dump spin axes orientations for easier restart
      call io_dump_spin('dump_spin.dat',ntp,iseed)

      nleft = ntp
      i1st = 0

c***************here's the big loop *************************************
      write(*,*) ' ************** MAIN LOOP ****************** '

      do while ((t.le.tstop).and.((ntp.eq.0).or.(nleft.gt.0)))

        call rmvs3_step(i1st,t,nbod,ntp,mass,j2rp2,j4rp4,
     &    xh,yh,zh,vxh,vyh,vzh,xht,yht,zht,vxht,vyht,
     &    vzht,istat,rstat,dt)

        t = t + dt

c  additional disruption/discard criteria
        if (btest(iflgchk,7)) then	! bit 7 is set
          if (t.ge.treorient) then
            call disrupt(t,ntp,dtreorient,tau_disr,iseed,istat,rstat,
     :        disrupted)
          endif
        endif
        call discard_meana(t,ntp,a_mean1,a_mean2,istat,rstat)

        if(btest(iflgchk,4))  then    ! bit 4 is set
          call discard(t,dt,nbod,ntp,mass,xh,yh,zh,vxh,vyh,vzh,
     &      xht,yht,zht,vxht,vyht,vzht,rmin,rmax,rmaxu,
     &      qmin,lclose,rplsq,istat,rstat)
          call io_discard_write(1,t,nbod,ntp,xh,yh,zh,vxh,vyh,
     &      vzh,xht,yht,zht,vxht,vyht,vzht,istat,rstat,iud,
     &      'discard.out',fopenstat,nleft)
        else
          nleft = ntp
        endif

c if it is time, output orb. elements, 
        if(t .ge. tout) then 

          if (btest(iflgchk,8)) then  	! bit 8 is set (real*8 dump)
            call io_write_frame_r8(t,nbod,ntp,mass,xh,yh,zh,
     &        vxh,vyh,vzh,xht,yht,zht,vxht,vyht,vzht,istat,
     &        outfile,iub,fopenstat)
          else if (btest(iflgchk,0)) then	! bit 0 is set
            call  io_write_frame(t,nbod,ntp,mass,xh,yh,zh,vxh,
     &        vyh,vzh,xht,yht,zht,vxht,vyht,vzht,istat,outfile,
     &        iub,fopenstat)
          else if (btest(iflgchk,1)) then	! bit 1 is set
            call io_write_frame_r(t,nbod,ntp,mass,xh,yh,zh,
     &        vxh,vyh,vzh,xht,yht,zht,vxht,vyht,vzht,istat,
     &        outfile,iub,fopenstat)
          endif

          tout = tout + dtout
        endif

c If it is time, do a dump
        if(t.ge.tdump) then

          tfrac = (t-t0)/(tstop-t0)
          write(*,998) t,tfrac,nleft
 998      format(' Time = ',1p1e12.5,': fraction done = ',0pf5.3,
     &      ': Number of active tp =',i4)
          call io_dump_pl('dump_pl.dat',nbod,mass,xh,yh,zh,
     &      vxh,vyh,vzh,lclose,iflgchk,rplsq,j2rp2,j4rp4)
          call io_dump_tp('dump_tp.dat',ntp,xht,yht,zht,
     &      vxht,vyht,vzht,istat,rstat)
          call io_dump_param('dump_param.dat',t,tstop,dt,dtout,
     &      dtdump,iflgchk,rmin,rmax,rmaxu,qmin,lclose,outfile)
          tdump = tdump + dtdump

          if(btest(iflgchk,2))  then    ! bit 2 is set
            call anal_energy_write(t,nbod,mass,j2rp2,j4rp4,
     &        xh,yh,zh,vxh,vyh,vzh,iue,fopenstat,eoff)
          endif
          if(btest(iflgchk,3))  then    ! bit 3 is set
            call anal_jacobi_write(t,nbod,ntp,mass,xh,yh,zh,vxh,
     &        vyh,vzh,xht,yht,zht,vxht,vyht,vzht,istat,2,
     &        iuj,fopenstat)
          endif

        endif

c If it is time, save data to the filter buffer,
c if buffer is filled up, write out filtered orbital elements of test particles.
c Recalculate parameters for seasonal Yarkovky effect, which depends on it.

        if (t.ge.tfilter) then
          call io_write_filter(t,nbod,ntp,mass,xh,yh,zh,
     &      vxh,vyh,vzh,xht,yht,zht,vxht,vyht,vzht,istat,
     &      outfilterfile,iuf,fopenstat)

          call yarko_seasonal(ntp,mass)

          tfilter = tfilter + dtfilter
        endif

c Check, if osculating or mean elemens have changed, fill the proper elements
c buffer and eventually write the output.

        if (t.ge.tproper) then
          call io_write_proper(t,nbod,ntp,istat,outproperfile,iup,
     :      fopenstat)

          tproper = tproper + dtproper
        endif

c If it is time, reorient spin axes and discard test particles;
c spin axes array s(NTPMAX,3) is given in common block /yarko/

        if (t.ge.treorient) then
          call reorient(t,ntp,dtreorient,tau_reor,iseed,reoriented)
          call reorient(t,ntp,dtreorient,tau_yorp,iseed,yorpiented)

c  write reorient.out and disrupt.out files
          if ((btest(iflgchk,6)).and.((reoriented).or.(yorpiented)))
     &      then	! bit 6 is set
            call reorient_write(t,ntp,iur,fopenstat)
          endif
          if ((btest(iflgchk,7)).and.(disrupted)) then	! bit 7 is set
            call disrupt_write(t,ntp,istat,rstat,iui,fopenstat)
          endif

          call io_dump_spin('dump_spin.dat',ntp,iseed)

          treorient = treorient + dtreorient
        endif

      enddo
c********** end of the big loop from time 't0' to time 'tstop'

c  Do a final dump for possible resumption later 

      call io_dump_pl('dump_pl.dat',nbod,mass,xh,yh,zh,
     &  vxh,vyh,vzh,lclose,iflgchk,rplsq,j2rp2,j4rp4)
      call io_dump_tp('dump_tp.dat',ntp,xht,yht,zht,
     &  vxht,vyht,vzht,istat,rstat)
      call io_dump_param('dump_param.dat',t,tstop,dt,dtout,
     &  dtdump,iflgchk,rmin,rmax,rmaxu,qmin,lclose,outfile)

      call io_close(iub,iuf,iup)
      call util_exit(0)
      end    ! swift_rmvs3_fp_ye.f
c---------------------------------------------------------------------

